/*
 *    Calendula - An assistant for personal medication management.
 *    Copyright (C) 2016 CITIUS - USC
 *
 *    Calendula is free software; you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation; either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    This program is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with this software.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'



repositories {
    mavenCentral()
    jcenter()
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://jitpack.io' }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// read keystore for app signing
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = null
if (keystorePropertiesFile.exists()) {
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    return formattedDate
}

def getCustomProguardFiles() {
    return fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 31
        versionName "2.5-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        applicationId "es.usc.citius.servando.calendula"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties != null ? keystoreProperties['keyAlias'] : null
            keyPassword keystoreProperties != null ? keystoreProperties['keyPassword'] : null
            storeFile keystoreProperties != null ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties != null ? keystoreProperties['storePassword'] : null
        }
    }


    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles getCustomProguardFiles()
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles getCustomProguardFiles()
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
        }
        beta {
            minifyEnabled false
            versionNameSuffix "-build" + getDate()
        }
        alpha {
            minifyEnabled false
            applicationIdSuffix ".alpha"
            signingConfig signingConfigs.release
            versionNameSuffix "-" + getDate()
        }
    }

    flavorDimensions "buildType"

    productFlavors {
        develop {
            applicationIdSuffix ".dev"
            dimension "buildType"
        }
        product {
            dimension "buildType"
        }
        ci {
            dimension "buildType"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
    compileOptions.incremental = false
}

dependencies {

    /********** DEBUGGING **********/
    // Chrome debug bridge
    debugCompile 'com.facebook.stetho:stetho:1.5.0'

    // Memory leak debugging
    //  1.5.2 held back because of a bug with gradle 3.0
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    alphaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    betaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    androidTestCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'


    /********** SUPPORT **********/
    // Multidex support
    compile 'com.android.support:multidex:1.0.2'

    // Support libs
    compile "com.android.support:appcompat-v7:$libVersions.android.support"
    compile "com.android.support:cardview-v7:$libVersions.android.support"
    compile "com.android.support:design:$libVersions.android.support"
    compile "com.android.support:gridlayout-v7:$libVersions.android.support"
    compile "com.android.support:palette-v7:$libVersions.android.support"
    compile "com.android.support:preference-v7:$libVersions.android.support"
    compile "com.android.support:recyclerview-v7:$libVersions.android.support"
    compile "com.android.support:support-annotations:$libVersions.android.support"
    compile "com.android.support:support-v4:$libVersions.android.support"

    // Testing dependencies
    //  Made explicit to avoid conflicts with other testing libs
    androidTestCompile "com.android.support:appcompat-v7:$libVersions.android.support"
    androidTestCompile "com.android.support:design:$libVersions.android.support"
    androidTestCompile "com.android.support:recyclerview-v7:$libVersions.android.support"
    androidTestCompile "com.android.support:support-annotations:$libVersions.android.support"
    androidTestCompile "com.android.support:support-v4:$libVersions.android.support"


    /********** TESTING **********/
    // JUnit
    testCompile 'junit:junit:4.12'

    // Mockito
    testCompile "org.mockito:mockito-core:2.8.47"

    // Support testing
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:3.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0'
    androidTestCompile 'com.android.support.test:rules:1.0.0'
    androidTestCompile 'com.android.support.test:runner:1.0.0'


    /********** UTILITIES **********/
    // HTML parsing
    compile 'org.jsoup:jsoup:1.10.2'

    // Event bus
    compile 'org.greenrobot:eventbus:3.0.0'

    // Jobs
    compile 'com.evernote:android-job:1.1.11'

    // JSON serializing-deserializing
    compile 'com.google.code.gson:gson:2.8.1'

    // Lang utilities
    //  Used for Levenshtein distance, etc
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-text:1.1'

    // QR code scanning
    compile 'com.google.zxing:core:3.3.0'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0@aar'

    // Recurrence processing
    compile files('libs/rfc2445.jar')

    // SQLite ORM
    compile 'com.j256.ormlite:ormlite-android:5.0'

    // Time and date handling
    compile 'com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.6.0'
    compile 'joda-time:joda-time:2.9.7'

    // View/resource binding
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'com.jakewharton:butterknife:8.8.1'


    /********** VIEWS AND VIEW UTILITIES **********/
    // About page with libraries
    compile('com.mikepenz:aboutlibraries:5.2.5@aar') {
        transitive = true
    }

    // Calendar views
    compile 'com.roomorama:caldroid:3.0.1'
    compile 'com.squareup:android-times-square:1.6.4@aar'

    // Circular progress "pie" view
    //  Used for pill picker
    compile 'com.github.filippudak.progresspieview:library:1.0.4'

    // Dialogs with material style
    compile 'com.github.javiersantos:MaterialStyledDialogs:2.0'

    // GIF ImageView
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.3'

    // Icons
    compile 'com.mikepenz:community-material-typeface:1.9.32.2@aar'
    compile 'com.mikepenz:google-material-typeface:2.1.2.1@aar'
    compile 'com.mikepenz:iconics-core:2.7.1@aar'

    // Image loading
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Intro slides
    compile 'com.heinrichreimersoftware:material-intro:1.5.8'

    // Material style utilities
    compile('com.mikepenz:materialize:0.2.7@aar') {
        transitive = true
    }

    // Picker DialogFragments
    compile 'com.code-troopers.betterpickers:library:3.1.0'

    // Round image view
    compile 'com.makeramen:roundedimageview:1.5.0'

    // Tab strip
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    // Sliding left drawer
    compile('com.mikepenz:materialdrawer:4.4.1@aar') {
        transitive = true
    }

    // Recyclerview adapters
    compile('com.mikepenz:fastadapter:2.1.5@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'
    compile 'com.mikepenz:fastadapter-extensions:2.1.0@aar'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"


}
